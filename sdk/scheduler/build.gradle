plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.3'
}
apply plugin: 'application'

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    repositories {
        maven {
            if(project.version.endsWith('-SNAPSHOT')) {
                url "s3://downloads.mesosphere.io/maven-snapshot/"
            } else {
                url "s3://downloads.mesosphere.io/maven/"
            }
            credentials(AwsCredentials) {
                accessKey System.env['AWS_ACCESS_KEY_ID']
                secretKey System.env['AWS_SECRET_ACCESS_KEY']
            }
        }
        publications {
            mavenJava(MavenPublication) {
                groupId project.group
                artifactId 'scheduler'
                version project.version

                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
    }
}

configurations {
    runtime.exclude group: "org.slf4j", module: "slf4j-log4j12"
}
ext {
    jacksonVer = "2.6.3"
    curatorVer = "2.9.1"
    httpClientVer = "4.5.2"
    jerseyVer = "2.23"
    elVer = "2.2.4"
    bouncyCastleVer = "1.57"
    dropWizardMetricsVer = "3.2.5"
}

dependencies {
    compile project(':common')
    compile "com.fasterxml.jackson.datatype:jackson-datatype-guava:${jacksonVer}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVer}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVer}"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVer}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVer}"
    compile 'com.hubspot.jackson:jackson-datatype-protobuf:0.9.9-preJackson2.7-proto3'
    compile 'com.googlecode.protobuf-java-format:protobuf-java-format:1.4'
    compile 'com.github.spullara.mustache.java:compiler:0.9.2'
    compile 'com.google.code.findbugs:annotations:3.0.1u2'
    compile 'commons-collections:commons-collections:3.2.2'
    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.antlr:antlr4-runtime:4.5.1-1'
    compile 'javax.ws.rs:javax.ws.rs-api:2.0.1'
    compile "org.apache.curator:curator-framework:${curatorVer}"
    compile "org.apache.curator:curator-recipes:${curatorVer}"
    compile "org.apache.curator:curator-test:${curatorVer}"
    compile "org.apache.httpcomponents:httpclient:${httpClientVer}"
    compile "org.apache.httpcomponents:fluent-hc:${httpClientVer}"
    compile 'org.json:json:20160212'
    compile 'com.googlecode.java-diff-utils:diffutils:1.3.0'
    compile "org.glassfish.jersey.containers:jersey-container-jetty-http:${jerseyVer}"
    compile "org.glassfish.jersey.containers:jersey-container-servlet-core:${jerseyVer}"
    compile "org.glassfish.jersey.media:jersey-media-json-jackson:${jerseyVer}"
    compile "org.glassfish.jersey.media:jersey-media-multipart:${jerseyVer}"
    compile 'org.eclipse.jetty:jetty-servlet:9.4.9.v20180320'
    compile 'org.hibernate:hibernate-validator:5.3.2.Final'
    compile "javax.el:javax.el-api:${elVer}"
    compile "org.glassfish.web:javax.el:${elVer}"
    compile 'com.mesosphere:mesos-http-adapter:0.4.1'
    compile('com.auth0:java-jwt:3.2.0') {
        // JWT package comes with jackson version 2.8.4 which doesn't work properly with
        // scheduler specification classes.
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }
    compile "org.bouncycastle:bcprov-jdk15on:${bouncyCastleVer}"
    compile "org.bouncycastle:bcpkix-jdk15on:${bouncyCastleVer}"
    compile 'com.j2html:j2html:1.2.0'
    compile "io.dropwizard.metrics:metrics-core:${dropWizardMetricsVer}"
    compile "io.dropwizard.metrics:metrics-servlet:${dropWizardMetricsVer}"
    compile "io.dropwizard.metrics:metrics-servlets:${dropWizardMetricsVer}"
    compile 'com.codahale.metrics:metrics-jetty9:3.0.2'
    compile 'io.prometheus:simpleclient_dropwizard:0.0.26'
    compile 'io.prometheus:simpleclient_servlet:0.0.26'
    compile 'com.readytalk:metrics3-statsd:4.2.0'
    testCompile 'org.hamcrest:hamcrest-all:1.3' // note: must be above junit
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.9.5'
}

distributions {
    main {
        mainClassName = ''
        baseName = 'sdk'
    }
}

check.finalizedBy jacocoTestReport

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

shadowJar {
    classifier = 'uber'

    mergeServiceFiles()

    relocate ('com', 'sdk.com') {
        exclude 'com/mesosphere/**'
    }
    relocate 'edu', 'sdk.edu'
    relocate 'io', 'sdk.io'
    relocate 'net', 'sdk.net'
    relocate 'org', 'sdk.org'

    relocate 'difflib', 'sdk.difflib'
    relocate 'j2html', 'sdk.j2html'
    relocate 'javassist', 'sdk.javassist'
    relocate 'javax', 'sdk.javax'
    relocate 'jersey', 'sdk.jersey'
    relocate 'jline', 'sdk.jline'

    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}